if (Boolean.valueOf(rootProject.ext.isModuleThread)) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        if (Boolean.valueOf(rootProject.ext.isModuleThread)) {
            // 当作为application运行时需要application id
            applicationId "com.xxd.thread"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        resourcePrefix "${project.name}_"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        // 解决ktx的编译问题，默认使用的是1.6
        jvmTarget = "1.8"
    }

    /*
    * java插件引入了一个概念叫做SourceSets，通过修改SourceSets中的属性，
    * 可以指定哪些源文件（或文件夹下的源文件）要被编译，
    * 哪些源文件要被排除。
    * */
    sourceSets {
        main {
            if (Boolean.valueOf(rootProject.ext.isModuleThread)) {//apk
                // module 单独作为application时，使用此manifest
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                /*java {
                    //library模式下，排除java/debug文件夹下的所有文件
                    exclude '*module'
                }*/
            }
        }
    }
    namespace 'com.xxd.thread'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // 所有的module公共依赖
    implementation project(":common")

    kapt "com.alibaba:arouter-compiler:${versions.arouterCompilerVersion}"

    // 测试模块不能放在common中，引用不到
    testImplementation "junit:junit:${versions.junitVersion}"
    androidTestImplementation "androidx.test.ext:junit:${versions.junitAndroidVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espressoCoreVersion}"
}

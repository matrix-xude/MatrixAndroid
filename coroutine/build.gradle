if (Boolean.valueOf(rootProject.ext.isModuleCoroutine)) {
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {

        if (Boolean.valueOf(rootProject.ext.isModuleCoroutine)) {
            // 当作为application运行时需要application id
            applicationId "com.xxd.coroutine"
        }

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        // 约束资源名字前缀
        resourcePrefix "${project.name}_"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            if (Boolean.valueOf(rootProject.ext.isModuleCoroutine)) {//apk
                // module 单独作为application时，使用此manifest
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
    namespace 'com.xxd.coroutine'
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // 所有的module公共依赖
    implementation project(":common")

    kapt "com.alibaba:arouter-compiler:${versions.arouterCompilerVersion}"

    // 测试模块不能放在common中，引用不到
    testImplementation "junit:junit:${versions.junitVersion}"
    androidTestImplementation "androidx.test.ext:junit:${versions.junitAndroidVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espressoCoreVersion}"
}